;PARTSWAP.ASM
;1581 PARTITION LIST AND SWAP
;AKX 2021
;---------------------------------------
;SET UP BASIC SYS
         *= $0801

         ;END $080C
         ;LINE $000A
         ;SYS (TOKEN $9E)
         ;" 4096"
         ;END LINE (000)
         .BYTE $0C,$08,$0A,$00,$9E,$20
         .BYTE $34,$30,$39,$36,$00,$00
         .BYTE $00

;---------------------------------------
;VARIABLES
         *= $0820
DRIVE    .BYTE $00
TYPE     .BYTE $00
STAT     .BYTE $00
FOUND    .BYTE $00
PRES     .BYTE $00
INDIR    .BYTE $00
YY       .BYTE $00
NUMPART  .BYTE $00
DIRPOSL  .BYTE $00
DIRPOSH  .BYTE $00
DIRBUFF  .BYTE $00,$00,$00,$00,$00,$00
         .BYTE $00,$00,$00,$00,$00,$00
         .BYTE $00,$00,$00,$00,$00,$00
         .BYTE $00,$00,$00,$00

NAMEBUFF .BYTE $00,$00,$00,$00,$00,$00
         .BYTE $00,$00,$00,$00,$00,$00
         .BYTE $00,$00,$00,$00

XTEN     .BYTE $00,$0A,$14,$1E,$28,$32
         .BYTE $3C,$46,$50,$5A

;---------------------------------------
;DATA

S1       .TEXT "           "
         .TEXT "FINDING 1581 DRIVE"
LS1      .BYTE $1D

S2       .TEXT "           "
         .TEXT "LISTING PARTITIONS"
LS2      .BYTE $1D

S3       .TEXT "           "
         .TEXT "CHANGING PARTITION"
LS3      .BYTE $1D

MSGNP    .TEXT "1581 DRIVE NOT PRESENT"
         .BYTE $0D
LMSGNP   .BYTE $17

RPMSG    .TEXT "1581 DRIVE FOUND ON"
         .TEXT " DEVICE"
         .BYTE $20,$0D
LRPMSG   .BYTE $1B

UDMSG    .TEXT "USE THIS DRIVE? (Y/N)"
         .BYTE $0D
LUDMSG   .BYTE $16

MRTYPE   .TEXT "M-R"
         .BYTE $C6,$E5,$01
LMRTYPE  .BYTE $06

PCMSG    .TEXT "CONTINUE? (Y/N)"
         .BYTE $0D
LPCMSG   .BYTE $10

ENTMSG   .TEXT "ENTER A PARTITION? "
         .TEXT "(Y/N)"
         .BYTE $0D
LENTMSG  .BYTE $19

PNMSG    .TEXT "TYPE QUIT TO EXIT"
         .TEXT "       "
         .BYTE $0D
         .TEXT "NAME OF PARTITION: "
LPNMSG   .BYTE $2C

ROOT     .TEXT "/"
         .BYTE $0D
LROOT    .BYTE $02

QUIT     .TEXT "QUIT"
LQUIT    .BYTE $04

NOPAMSG  .TEXT "NO PARTITIONS-"
         .TEXT "INITIALIZING..."
         .BYTE $0D
LNOPAMSG .BYTE $1E

AGMSG    .TEXT "CHANGE AGAIN? (Y/N)"
         .BYTE $0D
LAGMSG   .BYTE $14

DNAME    .TEXT "$0"
LDNAME   .BYTE $02

INIT     .TEXT "I0"
LINIT    .BYTE $02

CD       .TEXT "/0:"
LCD      .BYTE $03

;---------------------------------------
;BEGIN CODE
         *= $1000
;JUMP TABLE
         LDA #$00
         STA $D020    ;BORDER = BLACK
         STA $D021    ;BCKGND = BLACK
         LDA #$93
         JSR $FFD2    ;CLS
         LDA #$05
         JSR $FFD2    ;WHITE TEXT
         JSR SPROBE   ;PROBE DRIVS 8->30
         LDA FOUND
         CMP #$01
         BNE EXIT     ;IF NO 1581
         JSR CLS
CHGDIR   JSR SLIST    ;LIST PARTITIONS
         LDA STAT
         CMP #$4A
         BEQ EXIT     ;DRIVE NOT RDY
         JSR GPART    ;INPUT PARTITION
         LDA STAT
         CMP #$4A
         BEQ EXIT
         JSR CPART    ;CHANGE PARTITION
         LDA STAT
         CMP #$4A
         BEQ EXIT
         JMP AGAIN
EXIT     RTS          ;RETURN TO BASIC

AGAIN    LDY #$00
AGLOOP1  LDA AGMSG,Y
         JSR $FFD2
         INY
         CPY LAGMSG
         BNE AGLOOP1
AGLOOP2  JSR $FFE4
         CMP #$4E
         BEQ EXIT
         CMP #$59
         BNE AGLOOP2
         JSR CLS
         JMP CHGDIR

;---------------------------------------
;PROBE DRIVES 8->30 FOR 1581

SPROBE   LDA #$08     ;DEVICE 8
         STA DRIVE
         JSR PRINTS1
         LDX #$01
         LDY #$00
         JSR $FFF0
PROBE    JSR CHKPRES  ;IS DRV PRESENT?
         LDA PRES
         CMP #$00
         BNE PAHEAD   ;IF NOT, SKIP
         JSR OCOMM    ;OPEN COMM CHNL
         JSR GTYPE    ;GET DRIVE TYPE
         JSR CCOMM    ;CLOSE COMM CHNL
         LDA TYPE
         CMP #$08     ;8=1581,7=OTHER
         BEQ RPROBE   ;RETURN
PAHEAD   INC DRIVE
         LDA DRIVE
         CMP #$1F     ;DRIVE <31
         BNE PROBE
         JSR NPRES
         RTS

GTYPE    LDX #$0F
         JSR $FFC9    ;CHKOUT
         LDY #$00
GTLOOP   LDA MRTYPE,Y
         JSR $FFD2    ;CHROUT
         INY
         CPY LMRTYPE
         BNE GTLOOP
         JSR $FFCC    ;CLRCHN
         LDX #$0F
         JSR $FFC6    ;CHKIN
         JSR $FFCF    ;CHRIN
         TAY
         JSR $FFCC    ;CLRCHN
         TYA
         ADC #$00     ;PROTECT NULL$
         CMP #$FF     ;255=1581
         BEQ DPRES
         LDA #$07
         STA TYPE
         RTS
DPRES    LDA #$08
         STA TYPE
         RTS

NPRES    LDY #$00
NPLOOP   LDA MSGNP,Y
         JSR $FFD2    ;CHROUT
         INY
         CPY LMSGNP
         BNE NPLOOP
         RTS

RPROBE   LDA #$01     ;DRIVE FOUND
         STA FOUND
         LDY #$00
RPLOOP   LDA RPMSG,Y  ;PRINT MESSAGE
         JSR $FFD2
         INY
         CPY LRPMSG
         BNE RPLOOP
         TYA
         PHA
         LDA DRIVE
         JSR NUMCHAR  ;PRINT DRIVE #
         PLA
         TAY
         LDA RPMSG,Y
         JSR $FFD2    ;PRINT RETURN
         JSR RERR
         JSR UDRIVE
         RTS

UDRIVE   LDY #$00
UDLOOP   LDA UDMSG,Y
         JSR $FFD2
         INY
         CPY LUDMSG
         BNE UDLOOP
UDKBD    JSR $FFE4    ;SCAN KBD
         CMP #$4E     ;N
         BEQ UDNO
         CMP #$59     ;Y
         BEQ UDYES
         JMP UDKBD
UDNO     LDA #$00
         STA FOUND
         LDA #$0D
         JSR $FFD2
         JMP PAHEAD
UDYES    LDA #$0D
         JSR $FFD2
         RTS

PRINTS1  JSR POSSTAT
S1LOOP1  LDA S1,Y
         JSR $FFD2
         LDA #$03
         STA $D800,Y
         INY
         CPY LS1
         BNE S1LOOP1
         JSR CLRSTAT
         RTS

;---------------------------------------
;LIST PARTITIONS
SLIST    JSR PRINTS2
         JSR OCOMM
         LDA LDNAME
         LDX #<DNAME
         LDY #>DNAME
         JSR $FFBD    ;SETNAM
         LDA #$60     ;96,DRIVE,96
         LDX DRIVE
         LDY #$60
         JSR $FFBA    ;SETLFS
         JSR $FFC0    ;OPEN
         JSR RSTAT
         LDA STAT
         CMP #$4A     ;74 = NOT RDY
         BEQ LEXIT
         LDA STAT
         CMP #$20     ;NO ERRORS
         BCC PLIST
LEXIT    LDA #$60
         JSR $FFC3    ;CLOSE 96
         JSR CCOMM
         RTS

PLIST    CLC
         LDX #$01
         LDY #$00
         JSR $FFF0
         LDY #$00
         LDA #$20
PSPLOOP  JSR $FFD2
         INY
         CPY #$04
         BNE PSPLOOP
         LDA #$12     ;REV ON
         JSR $FFD2
         LDA #$22
         JSR $FFD2
         JSR FNDQUOTE
         JSR READHEAD
         LDA #$92     ;REV OFF
         JSR $FFD2
         LDA #$0D
         JSR $FFD2
PENTRY   LDA #$00
         STA DIRPOSL
         STA DIRPOSH
         STA YY
         STA NUMPART
DIRLOOP  JSR FNDQUOTE
         LDA INDIR
         CMP #$00
         BNE LEXIT
         JSR READDIR
         INC DIRPOSL
         LDA DIRPOSL
         CMP #$29     ;$29+$FF=296
         BNE DIRLOOP
         INC DIRPOSH
         LDA DIRPOSH
         CMP #$02     ;+$FF
         BNE DIRLOOP
         JMP LEXIT

FNDQUOTE LDA #$00
         STA INDIR
         LDX #$60
         JSR $FFC6    ;CHKIN
FNDLOOP  JSR $FFCF    ;CHRIN
         TAY
         JSR $FFB7
         BNE EXITDIR
         TYA
         CMP #$22
         BNE FNDLOOP
         RTS

READHEAD LDY #$16
HEADLOOP JSR $FFCF    ;CHRIN
         JSR $FFD2    ;CHROUT
         DEY
         BPL HEADLOOP
         JMP $FFCC    ;CLRCHN
         RTS

EXITDIR  INC INDIR
         JMP $FFCC
         RTS

READDIR  LDY #$15
RLOOP1   JSR $FFCF    ;CHRIN
         STA DIRBUFF,Y
         DEY
         BPL RLOOP1
         JSR $FFCC    ;CLRCHN
         LDA DIRBUFF+1
         CMP #$4D     ;"M" FROM ÃÂÍ
         BNE DIRRET
         INC YY
         INC NUMPART
         LDA YY
         CMP #$0E
         BEQ PRMTCONT
RDCONT   LDY #$05           ;?SPC(5)
         LDA #$20
RLOOP2   JSR $FFD2    ;CHROUT
         DEY
         BNE RLOOP2
         LDA #$22     ;?QUOTES
         JSR $FFD2    ;CHROUT
         LDY #$15
RLOOP3   LDA DIRBUFF,Y
         JSR $FFD2    ;CHROUT
         DEY
         BPL RLOOP3
         LDA #$0D     ;RETURN
         JSR $FFD2
DIRRET   RTS

PRMTCONT LDA #$00
         STA YY
         LDY #$00
PCLOOP   LDA PCMSG,Y
         JSR $FFD2    ;CHROUT
         INY
         CPY LPCMSG
         BNE PCLOOP
PCKBD    JSR $FFE4    ;SCAN KBD
         CMP #$4E     ;N
         BNE PCFWDY
         LDA #$28     ;SET END OF DIR
         STA DIRPOSL
         LDA #$01
         STA DIRPOSH
         RTS
PCFWDY   CMP #$59     ;Y
         BNE PCKBD
         JSR CLRDIR
         JMP RDCONT

CLRDIR   LDX #$02
         LDY #$00
         CLC
         JSR $FFF0
         LDX #$00
CLRLOOP1 LDY #$00
         LDA #$20
CLRLOOP2 JSR $FFD2
         INY
         CPY #$1B     ;27 SPACES
         BNE CLRLOOP2
         LDA #$0D     ;RETURN
         JSR $FFD2
         INX
         CPX #$0E     ;14 LINES
         BNE CLRLOOP1
         LDX #$02
         LDY #$00
         CLC
         JSR $FFF0
         RTS

PRINTS2  JSR POSSTAT
PS2L1    LDA S2,Y
         JSR $FFD2
         LDA #$03
         STA $D800,Y
         INY
         CPY LS2
         BNE PS2L1
         JSR CLRSTAT
         RTS

;---------------------------------------
;CHOOSE PARTITION

GPART    SEC
         JSR $FFF0
         TAY
         PHA
         TXA
         PHA
         JSR PRINTS3
         JSR SETBUFF
         PLA
         TAX
         PLA
         TAY
         CLC
         JSR $FFF0
         LDY #$00
GPLOOP1  LDA ENTMSG,Y
         JSR $FFD2
         INY
         CPY LENTMSG
         BNE GPLOOP1
GPLOOP2  JSR $FFE4   ;SCAN KBD
         CMP #$4E    ;N
         BNE GPFWY
         LDA #$4A
         STA STAT
         RTS
GPFWY    CMP #$59    ;Y
         BNE GPLOOP2
         LDA #$91
         JSR $FFD2
         LDY #$00
GPLOOP3  LDA PNMSG,Y
         JSR $FFD2
         INY
         CPY LPNMSG
         BNE GPLOOP3
         LDY #$00
GPLOOP4  LDA ROOT,Y
         JSR $FFD2
         INY
         CPY #$01
         BNE GPLOOP4
         LDA #$9D
GPLOOP5  JSR $FFD2
         DEY
         BNE GPLOOP5
GPLOOP6  JSR $FFCF
         CMP #$0D
         BEQ GPFWD
         STA NAMEBUFF,Y
         INY
         CPY #$10
         BNE GPLOOP6
GPFWD    RTS

SETBUFF  LDY #$00
SBLOOP1  LDA ROOT,Y
         STA NAMEBUFF,Y
         INY
         CPY LROOT
         BNE SBLOOP1
         LDA #$00
SBLOOP2  STA NAMEBUFF,Y
         INY
         CPY #$10
         BNE SBLOOP2
         RTS

PRINTS3  JSR POSSTAT
PS3L1    LDA S3,Y
         JSR $FFD2
         LDA #$03
         STA $D800,Y
         INY
         CPY LS3
         BNE PS3L1
         JSR CLRSTAT
         RTS

;---------------------------------------
;CHANGE PARTITION

CPART    LDY #$00    ;CHECK FOR QUIT
         LDA #$0D
         JSR $FFD2
CQLOOP   LDA NAMEBUFF,Y
         CMP QUIT,Y
         BNE CPROOT
         INY
         CPY LQUIT
         BNE CQLOOP
         LDA #$4A
         STA STAT
         RTS

CPROOT   LDY #$00    ;CHECK FOR ROOT
CRLOOP   LDA NAMEBUFF,Y
         CMP ROOT,Y
         BNE CPNAME
         INY
         CPY LROOT
         BNE CRLOOP
         JSR GOROOT
         RTS

CPNAME   LDA NUMPART
         CMP #$01    ;AT LEAST 1 PART
         BCC NOPARTS
         JSR OCOMM
         LDX #$0F
         JSR $FFC9   ;CHKOUT
         LDY #$00
CPNLOOP1 LDA CD,Y
         JSR $FFD2   ;CHROUT
         INY
         CPY LCD
         BNE CPNLOOP1
         LDY #$00
CPNLOOP2 LDA NAMEBUFF,Y
         CMP #$00
         BEQ CPNEXIT
         JSR $FFD2
         INY
         CPY #$10
         BNE CPNLOOP2
CPNEXIT  JSR $FFCC
         JSR RSTAT
         JSR CCOMM
         RTS

NOPARTS  LDY #$00
NPALOOP  LDA NOPAMSG,Y
         JSR $FFD2
         INY
         CPY LNOPAMSG
         BNE NPALOOP
GOROOT   JSR OCOMM
         LDX #$0F
         JSR $FFC9     ;CHKOUT
         LDY #$00
GRLOOP   LDA CD,Y
         JSR $FFD2     ;CHROUT
         INY
         CPY #$01
         BNE GRLOOP
         JSR $FFCC     ;CLRCHN
         JSR RSTAT
         JSR CCOMM
         RTS

;---------------------------------------
;GENERAL FUNCTIONS

CLS      LDA #$20
         LDY #$28
CLOOP1   STA $0400,Y
         INY
         BNE CLOOP1
CLOOP2   STA $0500,Y
         INY
         BNE CLOOP2
CLOOP3   STA $0600,Y
         INY
         BNE CLOOP3
CLOOP4   STA $0700,Y
         INY
         CPY #$C0
         BNE CLOOP4
         RTS

OCOMM    LDA #$00     ;NO FILENAME
         LDX #$00
         LDY #$00
         JSR $FFBD    ;SETNAM
         LDA #$0F
         LDX DRIVE
         LDY #$0F
         JSR $FFBA    ;SETLFS
         JSR $FFC0    ;OPEN
         RTS

CCOMM    LDA #$0F
         JSR $FFC3    ;CLOSE
         RTS

RSTAT    LDA #$01     ;SKIP OPEN/CLOSE
         JMP RSTAT1

RERR     JSR OCOMM
         LDA #$00
         PHA
RSTAT1   CMP #$01     ;SKIP IF RSTAT
         BNE RESKIP
         PHA
RESKIP   SEC
         JSR $FFF0    ;STORE CURSOR
         TYA
         PHA
         TXA
         PHA
         CLC
         LDX #$18
         LDY #$00
         JSR $FFF0    ;SET CURSOR
         LDA #$12
         JSR $FFD2
         LDA #$9E
         JSR $FFD2
         LDX #$0F
         JSR $FFC6    ;CHKIN
         JSR $FFCF    ;CHRIN
         PHA          ;PUSH TENS DIGIT
         JSR $FFD2    ;CHROUT
         JSR $FFCF    ;CHRIN
         JSR $FFD2    ;CHROUT
         PHA          ;PUSH ONES DIGIT
ELOOP    JSR $FFB7    ;READST
         BNE ECLOSE
         JSR $FFCF    ;CHRIN
         CMP #$0D
         BEQ ECLOSE
         JSR $FFD2    ;CHROUT
         JMP ELOOP
ECLOSE   SEC
         JSR $FFF0
         INY
PELOOP   LDA #$A0
         STA $07BF,Y
         LDA #$07
         STA $DBBF,Y
         INY
         CPY #$29
         BNE PELOOP
         PLA          ;PULL ONES DIGIT
         SEC
         SBC #$30
         STA STAT
         PLA          ;PULL TENS DIGIT
         SBC #$30
         TAY
         LDA XTEN,Y
         CLC
         ADC STAT
         STA STAT
         LDA #$92
         JSR $FFD2
         LDA #$05
         JSR $FFD2
         CLC
         PLA          ;PULL CURSOR POS
         TAX
         PLA
         TAY
         JSR $FFF0    ;PLOT-RETURN
         JSR $FFCC    ;CLRCHN
         PLA
         CMP #$00
         BNE RSEXIT   ;SKIP IF RSTAT
         JSR CCOMM
RSEXIT   RTS

CHKPRES  LDA #$00
         LDX #$00
         LDY #$00
         JSR $FFBD    ;SETNAM
         LDA #$01
         LDX DRIVE
         LDY #$01
         JSR $FFBA    ;SETLFS
         JSR $FFC0    ;OPEN
         LDA #$01
         JSR $FFC3    ;CLOSE
         JSR $FFB7    ;READST (WAS $90)
         STA PRES     ;ÓÔ=0 IF SUCCESS
         RTS

POSSTAT  LDA #$12
         JSR $FFD2
         LDY #$00
         LDX #$00
         CLC
         JSR $FFF0
         RTS

CLRSTAT  LDA #$A0
         STA $0400,Y
         LDA #$03
         STA $D800,Y
         INY
         CPY #$28
         BNE CLRSTAT
         LDA #$92
         JSR $FFD2
         RTS

NUMCHAR  CMP #$0A
         BCC NCAHEAD  ;IF <10 1 DIGIT
         TAX          ;STORE NUMBER
         LDY #$09
NCLOOP   CMP XTEN,Y   ;90->1X
         BCS TENS     ;IF XTEN,Y > A
         DEY
         JMP NCLOOP
TENS     TYA          ;INDEX Y=TENS
         CLC
         ADC #$30     ;CONV TO CHAR
         JSR $FFD2
         TXA          ;RETURN NUMBER
         SEC          ;SET CARRY
         SBC XTEN,Y   ;SUBTRACT TENS
         CLC
NCAHEAD  ADC #$30     ;OFFSET FOR CHAR
         JSR $FFD2
         RTS

;---------------------------------------
;END OF CODE

